plugins {
    id "java-library"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = "dev.nozyx"
version = "1.0.0"

repositories {
    mavenCentral()

    maven {
        url = "https://litarvan.github.io/maven"
    }
}

dependencies {
    api "fr.litarvan:openauth:1.1.6"
    api "fr.flowarg:flowupdater:1.9.2"
    api "fr.flowarg:openlauncherlib:3.2.11"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.register("generateSourcesJar", Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.register("generateJavadocJar", Jar) {
    archiveClassifier.set("javadoc")
    from tasks.javadoc
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(tasks.named("generateSourcesJar").get())
            artifact(tasks.named("generateJavadocJar").get())

            groupId = group
            artifactId = "craft4j"
            version = version
        }
    }
    repositories {
        maven {
            url = uri("$buildDir/repo")
        }
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "craft4j",
                "Implementation-Version": version,
                "Implementation-Vendor": "nozyx12"
        )
    }
}
